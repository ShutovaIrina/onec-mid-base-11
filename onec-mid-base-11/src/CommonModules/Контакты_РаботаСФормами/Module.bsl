#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	Если ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" 
		или ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" тогда
		ДобавитьКонтактноеЛицоПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента"
		или ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" тогда
		ВставитьКонтактноеЛицоПередСуммой(Форма);	
	ИначеЕсли ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" тогда
		ДобавитьКонтактноеЛицоПраво(Форма);	
		ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"),Форма.Элементы.ГруппаШапкаЛево);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.Контакты_СогласованнаяСкидка";
		ПолеВвода.Заголовок = "Согласованная скидка";
		ПолеВвода.УстановитьДействие("ПриИзменении","Контакты_СогласованнаяСкидкаПриИзменении"); 
		
		Команда = Форма.Команды.Добавить("Пересчитать");
		Команда.Заголовок = "Пересчитать";
		Команда.Действие = "КомандаПересчитать";
		КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитать", Тип ("КнопкаФормы"), Форма.Элементы.ГруппаШапкаЛево);
		КнопкаФормы.ИмяКоманды = "Пересчитать";
		КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;	
	КонецЕсли;
		
КонецПроцедуры


#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьКонтактноеЛицоПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип ("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Контакты_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ВставитьКонтактноеЛицоПередСуммой(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Контакты_КонтактноеЛицо";
	
КонецПроцедуры

#КонецОбласти 
